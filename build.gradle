plugins {
	id 'java'
	id 'org.springframework.boot' version '3.2.5'
	id 'io.spring.dependency-management' version '1.1.7'
    id 'jacoco'
}

group = 'com.coupon'
version = '0.0.1-SNAPSHOT'
description = 'Demo project for Spring Boot'

java {
	toolchain {
		languageVersion = JavaLanguageVersion.of(21)
	}
}

jacoco {
    toolVersion = "0.8.11"
}

test {
    useJUnitPlatform()
    finalizedBy jacocoTestReport
}

jacocoTestReport {
    dependsOn test

    reports {
        xml.required.set(true)
        csv.required.set(false)
        html.required.set(true)
        html.outputLocation = layout.buildDirectory.dir('reports/jacoco')
    }
}

sourceSets {
	integrationTest {
		java {
			srcDirs = ['src/integration-test/java']
		}
		resources {
			srcDirs = ['src/integration-test/resources']
		}
		compileClasspath += sourceSets.main.output + sourceSets.test.output
		runtimeClasspath += sourceSets.main.output + sourceSets.test.output
	}
}

configurations {
	integrationTestAnnotationProcessor.extendsFrom annotationProcessor
	integrationTestImplementation.extendsFrom testImplementation
	integrationTestRuntimeOnly.extendsFrom testRuntimeOnly
}

repositories {
	mavenCentral()
}

ext {
	mapstructVersion = "1.5.5.Final"
	springDocVersion = "2.3.0"
}

dependencies {
	// Spring Boot Core&Web
	implementation 'org.springframework.boot:spring-boot-starter-web'
	implementation 'org.springframework.boot:spring-boot-starter-validation'

	// Banco de Dados (JPA+H2)
	implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
	runtimeOnly 'com.h2database:h2'

	// Documentação (Swagger/OpenAPI)
	implementation "org.springdoc:springdoc-openapi-starter-webmvc-ui:${springDocVersion}"

	// Ferramentas e Utilitários (Lombok+MapStruct)
	compileOnly 'org.projectlombok:lombok'
	implementation "org.mapstruct:mapstruct:${mapstructVersion}"

	// Processadores de Anotação
	annotationProcessor 'org.projectlombok:lombok'
	annotationProcessor 'org.projectlombok:lombok-mapstruct-binding:0.2.0'
	annotationProcessor "org.mapstruct:mapstruct-processor:${mapstructVersion}"

	// Testes Unitários
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}

tasks.register('integrationTest', Test) {
	description = 'Executa os testes de integração localizados em src/integration-test.'
	group = 'verification'

	testClassesDirs = sourceSets.integrationTest.output.classesDirs
	classpath = sourceSets.integrationTest.runtimeClasspath

	useJUnitPlatform()

	testLogging {
		events "passed", "skipped", "failed"
	}

	outputs.upToDateWhen { false }
}

check.dependsOn integrationTest

tasks.named('test') {
	useJUnitPlatform()
}